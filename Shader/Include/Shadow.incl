#ifndef SHADOW_INCL
#define SHADOW_INCL

#include "Common.incl"
#include "CommonUBO.incl"

const float ShadowWeights5[5][5] =
{
    { 0.0,0.5,1.0,0.5,0.0 },
    { 0.5,1.0,1.0,1.0,0.5 },
    { 1.0,1.0,1.0,1.0,1.0 },
    { 0.5,1.0,1.0,1.0,0.5 },
    { 0.0,0.5,1.0,0.5,0.0 }
};

float GetBias(vec3 normal, vec3 light, float biasFactor, float biasMin)
{
	float cosAlpha = dot(normal, light);
	float sinAlpha = sqrt(1.0 - cosAlpha * cosAlpha);
	//return (0.0025 /*+ 0.0015 * cascade*/) * sinAlpha / cosAlpha;
	return max(biasFactor * sinAlpha / cosAlpha, biasMin);
	//float bias = 0.004 * (1.0 - abs(dot(normal, -lights[i].directionRAB.xyz)));  
	//float bias = 0.008;
}

float CalcShadowBasic(vec3 viewPos, mat4 shadowMat, sampler2D shadowMap, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	return posLCS.z <= texture(shadowMap, posLCS.xy).r ? 1 : 0;
} 

float CalcShadowLerp(vec3 viewPos, mat4 shadowMat, sampler2DShadow shadowMap, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	return textureProj(shadowMap, posLCS);
} 

float CalcShadowTap5(vec3 viewPos, mat4 shadowMat, sampler2D shadowMap, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec2 mapSize = textureSize(shadowMap, 0);
	//vec2 sampleScale = 1.f * posLCS.w / mapSize;
	vec2 sampleScale = 1.f / mapSize;
	const int steps = 2;
	const float depthRange = bias;
	float sumWeights = 0;
	float depthDiff = 0;
	for(int xIdx = -steps; xIdx <= steps; ++xIdx)
	{
		for(int yIdx = -steps; yIdx <= steps; ++yIdx)
		{
			float weight = ShadowWeights5[xIdx + steps][yIdx + steps];
			vec2 uv = posLCS.xy + vec2(xIdx, yIdx) * sampleScale;
			//if(uv.x < 0 || uv.x > 1 || uv.y < 0 || uv.y > 1)
			//	continue;				
			//depthDiff += (texture(shadowMap, uv).r - posLCS.z) * weight;
			depthDiff += clamp(texture(shadowMap, uv).r - posLCS.z, -depthRange, depthRange) * weight;
			//shadowFactor += posLCS.z <= texture(shadowMap, uv).r ? weight : 0;
			sumWeights += weight;
		}
	}
	depthDiff /= sumWeights;
	//return depthDiff >= 0 ? 1 : 0;
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
} 

float CalcShadowPoisson(vec3 viewPos, mat4 shadowMat, sampler2D shadowMap, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec2 mapSize = textureSize(shadowMap, 0);
	vec2 sampleScale = 1.5 / mapSize;
	const int sampleCount = 9;
	const float depthRange = bias;
	float depthDiff = 0;
	float randV = GetRandom(vec4(viewPos * 100, time));
	float randCos = abs(randV * 2 - 1) * 2 - 1; // [0,1] -> [-1, 0 ,1] -> [1, -1, 1]
	float randSin = sqrt(1 - randCos*randCos) * (randV > 0.5 ? -1 : 1);
	for(int sampleIdx = 0; sampleIdx < sampleCount; ++sampleIdx)
	{
		vec2 offset = PoissonDisk9[sampleIdx];
		// rotate
		offset = vec2(
			offset.x * randCos - offset.y * randSin,
			offset.x * randSin + offset.y * randCos
		);
		vec2 uv = posLCS.xy + offset * sampleScale;
		//depthDiff += (texture(shadowMap, uv).r - posLCS.z);
		depthDiff += clamp(texture(shadowMap, uv).r - posLCS.z, -depthRange, depthRange);
	}
	depthDiff /= float(sampleCount);
	//return depthDiff >= 0 ? 1 : 0;
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
}

float CalcShadowBasicArray(vec3 viewPos, mat4 shadowMat, sampler2DArray shadowMapArray, int layer, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	return posLCS.z <= texture(shadowMapArray, vec3(posLCS.xy, layer)).r ? 1 : 0;
} 

float CalcShadowPoissonArray(vec3 viewPos, mat4 shadowMat, sampler2DArray shadowMapArray, int layer, float bias)
{
	// convert to light clip space
	vec4 posLCS = shadowMat * vec4(viewPos, 1.f);
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec3 mapSize = textureSize(shadowMapArray, 0);
	vec2 sampleScale = 1.5 / mapSize.xy;
	const int sampleCount = 9;
	const float depthRange = bias;
	float depthDiff = 0;
	float randV = GetRandom(vec4(viewPos * 100, time));
	float randCos = abs(randV * 2 - 1) * 2 - 1; // [0,1] -> [-1, 0 ,1] -> [1, -1, 1]
	float randSin = sqrt(1 - randCos*randCos) * (randV > 0.5 ? -1 : 1);
	for(int sampleIdx = 0; sampleIdx < sampleCount; ++sampleIdx)
	{
		vec2 offset = PoissonDisk9[sampleIdx];
		// rotate
		offset = vec2(
			offset.x * randCos - offset.y * randSin,
			offset.x * randSin + offset.y * randCos
		);
		vec3 uv = vec3(posLCS.xy + offset * sampleScale, layer);
		depthDiff += clamp(texture(shadowMapArray, uv).r - posLCS.z, -depthRange, depthRange);
	}
	depthDiff /= float(sampleCount);
	//return depthDiff >= 0 ? 1 : 0;
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
}

float CalcShadowBasicCube(vec3 viewPos, mat4 lightProjRemapMat, mat4 shadowMat, samplerCube shadowMap, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	float viewZ = max(max(abs(posLVS.x), abs(posLVS.y)), abs(posLVS.z));
	vec4 posLCS = lightProjRemapMat * vec4(0, 0, -viewZ, 1);	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec3 uv = posLVS.xyz;
	return posLCS.z <= texture(shadowMap, uv).r ? 1 : 0;
} 

float CalcShadowPoissonCube(vec3 viewPos, mat4 lightProjRemapMat, mat4 shadowMat, samplerCube shadowMap, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	float viewZ = max(max(abs(posLVS.x), abs(posLVS.y)), abs(posLVS.z));
	vec4 posLCS = lightProjRemapMat * vec4(0, 0, -viewZ, 1);	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec3 forward = normalize(posLVS.xyz);
	//vec3 up = forward.z > 0.5 ? vec3(1, 0, 0) : vec3(0, 1, 0);
	vec3 up = forward.z > 0.5 ? vec3(1, 0, 0) : vec3(0, 0, 1);
	vec3 right = cross(forward, up);
	up = cross(right, forward);	
	vec2 mapSize = textureSize(shadowMap, 0);
	vec2 sampleScale = 3 / mapSize; // twice as normal, since our sample range is [-1, 1]
	const int sampleCount = 4;
	const float depthRange = bias;
	float depthDiff = 0;
	float randV = GetRandom(vec4(viewPos * 100, time));
	float randCos = abs(randV * 2 - 1) * 2 - 1; // [0,1] -> [-1, 0 ,1] -> [1, -1, 1]
	float randSin = sqrt(1 - randCos*randCos) * (randV > 0.5 ? -1 : 1);
	for(int sampleIdx = 0; sampleIdx < sampleCount; ++sampleIdx)
	{
		vec2 offset = PoissonDisk4[sampleIdx];
		// rotate
		offset = vec2(
			offset.x * randCos - offset.y * randSin,
			offset.x * randSin + offset.y * randCos
		) * sampleScale;
		vec3 uv = forward + offset.x * right + offset.y * up;
		//depthDiff += (texture(shadowMap, uv).r - posLCS.z);
		depthDiff += clamp(texture(shadowMap, uv).r - posLCS.z, -depthRange, depthRange);
	}
	depthDiff /= float(sampleCount);
	//return depthDiff >= 0 ? 1 : 0;
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
}

float CalcShadowBasicCubeArray(vec3 viewPos, mat4 lightProjRemapMat, mat4 shadowMat, samplerCubeArray shadowMapArray, int layer, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	float viewZ = max(max(abs(posLVS.x), abs(posLVS.y)), abs(posLVS.z));
	vec4 posLCS = lightProjRemapMat * vec4(0, 0, -viewZ, 1);	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec4 uv = vec4(posLVS.xyz, layer);
	return posLCS.z <= texture(shadowMapArray, uv).r ? 1 : 0;
} 

float CalcShadowPoissonCubeArray(vec3 viewPos, mat4 lightProjRemapMat, mat4 shadowMat, samplerCubeArray shadowMapArray, int layer, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	float viewZ = max(max(abs(posLVS.x), abs(posLVS.y)), abs(posLVS.z));
	vec4 posLCS = lightProjRemapMat * vec4(0, 0, -viewZ, 1);	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec3 forward = normalize(posLVS.xyz);
	//vec3 up = forward.z > 0.5 ? vec3(1, 0, 0) : vec3(0, 1, 0);
	vec3 up = forward.z > 0.5 ? vec3(1, 0, 0) : vec3(0, 0, 1);
	vec3 right = cross(forward, up);
	up = cross(right, forward);	
	vec3 mapSize = textureSize(shadowMapArray, 0);
	vec2 sampleScale = 3 / mapSize.xy; // twice as normal, since our sample range is [-1, 1]
	const int sampleCount = 4;
	const float depthRange = bias;
	float depthDiff = 0;
	float randV = GetRandom(vec4(viewPos * 100, time));
	float randCos = abs(randV * 2 - 1) * 2 - 1; // [0,1] -> [-1, 0 ,1] -> [1, -1, 1]
	float randSin = sqrt(1 - randCos*randCos) * (randV > 0.5 ? -1 : 1);
	for(int sampleIdx = 0; sampleIdx < sampleCount; ++sampleIdx)
	{
		vec2 offset = PoissonDisk4[sampleIdx];
		// rotate
		offset = vec2(
			offset.x * randCos - offset.y * randSin,
			offset.x * randSin + offset.y * randCos
		) * sampleScale;
		vec4 uv = vec4(forward + offset.x * right + offset.y * up, layer);
		depthDiff += clamp(texture(shadowMapArray, uv).r - posLCS.z, -depthRange, depthRange);
	}
	depthDiff /= float(sampleCount);
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
}

const float tetraA = 0.57735026f;
const float tetraB = 0.81649661f;
// in view space
const vec3 tetrahedronDirs[4] = vec3[]
(
	vec3(0.0, -tetraA, -tetraB),	// forward
	vec3(0.0, -tetraA, tetraB),		// backward
	vec3(tetraB, tetraA, 0.0),		// right
	vec3(-tetraB, tetraA, 0.0)		// left
);

int GetTetrahedronIndex(vec4 posLVS)
{
	float dots[4];
	dots[0] = dot(posLVS.xyz, tetrahedronDirs[0]);
	dots[1] = dot(posLVS.xyz, tetrahedronDirs[1]);
	dots[2] = dot(posLVS.xyz, tetrahedronDirs[2]);
	dots[3] = dot(posLVS.xyz, tetrahedronDirs[3]);
	
	int idx = 0;
	float maxDot = max(dots[0], max(dots[1], max(dots[2], dots[3])));
	if(maxDot == dots[1]) idx = 1;
	else if(maxDot == dots[2]) idx = 2;
	else if(maxDot == dots[3]) idx = 3;
	
	return idx;	
}

vec4 GetTetrahedronProj(vec4 posLVS, mat4 lightProjRemapMat[4])
{	
	return lightProjRemapMat[GetTetrahedronIndex(posLVS)] * posLVS;	
}

float CalcShadowBasicTetrahedronSingle(vec4 posLVS, mat4 lightProjRemapMat, mat4 shadowMat, sampler2D shadowMap, float bias)
{	
	vec4 posLCS = lightProjRemapMat * posLVS;	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec2 uv = posLCS.xy;
	return posLCS.z <= texture(shadowMap, uv).r ? 1 : 0;
} 

float CalcShadowBasicTetrahedron(vec3 viewPos, mat4 lightProjRemapMat[4], mat4 shadowMat, sampler2D shadowMap, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	
	vec4 posLCS = GetTetrahedronProj(posLVS, lightProjRemapMat);	
	posLCS.z -= posLCS.w * bias;
	posLCS /= posLCS.w;
	vec2 uv = posLCS.xy;
	return posLCS.z <= texture(shadowMap, uv).r ? 1 : 0;
} 

float CalcShadowPoissonTetrahedron(vec3 viewPos, mat4 lightProjRemapMat[4], mat4 shadowMat, sampler2D shadowMap, float bias)
{
	// convert to light view space
	vec4 posLVS = shadowMat * vec4(viewPos, 1.f);
	vec3 forward = normalize(posLVS.xyz);
	vec3 up = forward.z > 0.5 ? vec3(1, 0, 0) : vec3(0, 0, 1);
	vec3 right = cross(forward, up);
	up = cross(right, forward);	
	vec2 mapSize = textureSize(shadowMap, 0);
	vec2 sampleScale = 3 / mapSize; // twice as normal, since our sample range is [-1, 1]
	const int sampleCount = 4;
	const float depthRange = bias;
	float depthDiff = 0;
	float randV = GetRandom(vec4(viewPos * 100, time));
	float randCos = abs(randV * 2 - 1) * 2 - 1; // [0,1] -> [-1, 0 ,1] -> [1, -1, 1]
	float randSin = sqrt(1 - randCos*randCos) * (randV > 0.5 ? -1 : 1);
	for(int sampleIdx = 0; sampleIdx < sampleCount; ++sampleIdx)
	{
		vec2 offset = PoissonDisk4[sampleIdx];
		// rotate
		offset = vec2(
			offset.x * randCos - offset.y * randSin,
			offset.x * randSin + offset.y * randCos
		) * sampleScale;
		vec3 samplePosLVS = posLVS.xyz + offset.x * right + offset.y * up;
		vec4 posLCS = GetTetrahedronProj(vec4(samplePosLVS, posLVS.w), lightProjRemapMat);
		posLCS.z -= posLCS.w * bias;
		posLCS /= posLCS.w;
		vec2 uv = posLCS.xy;
		depthDiff += clamp(texture(shadowMap, uv).r - posLCS.z, -depthRange, depthRange);
	}
	depthDiff /= float(sampleCount);
	
	return smoothstep(-depthRange * 0.5f, depthRange * 0.5f, depthDiff);
}

float CalcShadowFog(vec3 viewPos, vec3 light, mat4 shadowMat, sampler2D shadowMap, float bias)
{
	return CalcShadowBasic(viewPos, shadowMat, shadowMap, bias);
}

float CalcShadow(vec3 viewPos, vec3 normal, vec3 light, mat4 shadowMat, sampler2D shadowMap, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	//return CalcShadowTap5(viewPos, shadowMat, shadowMap, bias);
	return CalcShadowPoisson(viewPos, shadowMat, shadowMap, bias);
	//return CalcShadowBasic(viewPos, shadowMat, shadowMap, bias);
	//return CalcShadowLerp(viewPos, shadowMat, shadowMap, bias);
}

float CalcShadowArray(vec3 viewPos, vec3 normal, vec3 light, mat4 shadowMat, sampler2DArray shadowMapArray, int layer, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	return CalcShadowPoissonArray(viewPos, shadowMat, shadowMapArray, layer, bias);
	//return CalcShadowBasicArray(viewPos, shadowMat, shadowMapArray, layer, bias);
}

float CalcShadowCubeFog(vec3 viewPos, vec3 light, mat4 lightProjRemapMat, mat4 shadowMat, samplerCube shadowMap, float bias)
{
	return CalcShadowBasicCube(viewPos, lightProjRemapMat, shadowMat, shadowMap, bias);
}

float CalcShadowCube(vec3 viewPos, vec3 normal, vec3 light, mat4 lightProjRemapMat, mat4 shadowMat, samplerCube shadowMap, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	//return CalcShadowTap5(viewPos, shadowMat, shadowMap, bias);
	return CalcShadowPoissonCube(viewPos, lightProjRemapMat, shadowMat, shadowMap, bias);
	//return CalcShadowBasicCube(viewPos, lightProjRemapMat, shadowMat, shadowMap, bias);
}

float CalcShadowCubeArray(vec3 viewPos, vec3 normal, vec3 light, mat4 lightProjRemapMat, mat4 shadowMat, samplerCubeArray shadowMapArray, int layer, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	return CalcShadowPoissonCubeArray(viewPos, lightProjRemapMat, shadowMat, shadowMapArray, layer, bias);
	//return CalcShadowBasicCubeArray(viewPos, lightProjRemapMat, shadowMat, shadowMapArray, layer, bias);
}

float CalcShadowTetrahedron(vec3 viewPos, vec3 normal, vec3 light, mat4 lightProjRemapMat[4], mat4 shadowMat, sampler2D shadowMap, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	//return CalcShadowPoissonTetrahedron(viewPos, lightProjRemapMat, shadowMat, shadowMap, bias);
	return CalcShadowBasicTetrahedron(viewPos, lightProjRemapMat, shadowMat, shadowMap, bias);
}

float CalcShadowTetrahedronSingle(vec4 posLVS, vec3 normal, vec3 light, mat4 lightProjRemapMat, mat4 shadowMat, sampler2D shadowMap, float biasFactor, float biasMin)
{
	float bias = GetBias(normal, light, biasFactor, biasMin);
	return CalcShadowBasicTetrahedronSingle(posLVS, lightProjRemapMat, shadowMat, shadowMap, bias);
}

#endif